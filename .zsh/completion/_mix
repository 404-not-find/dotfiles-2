#compdef mix

_arguments \
  '1:task name:->tasks' \
  '2:task argument:->arguments' \
  '*:: :->rest'

local candidates

case "$state" in
  tasks)
    candidates=(${(f)"$(mix help | tail -n+2 | grep '^mix ' | cut -b5-)"})
    candidates=(${candidates/ *\# /:})
    _describe 'tasks' candidates
    ;;
  arguments)
    case $words[2] in
      archive.uninstall)
        candidates=(${(f)"$(mix archive | grep '^* ' | cut -b3-)"})
        _describe 'archives' candidates
        ;;
      help)
        candidates=(${(f)"$(mix help | tail -n+2 | grep '^mix ' | cut -b5-)"})
        candidates=(${candidates/ *\# /:})
        _describe 'tasks' candidates
        ;;
      hex.install)
        if [[ -n $words[3] ]]; then
          candidates=(${(f)"$(mix hex.search $words[3] | tail -n+2 | sed 's/  http.*$//')"})
          candidates=(${candidates/[[:space:]]##/:})
          _describe 'hex archives' candidates
        fi
    esac
esac
